buildscript {
    dependencies {
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0-beta1'
        classpath 'commons-httpclient:commons-httpclient:3.1'
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
    id 'com.github.jruby-gradle.base' version '0.1.5'
}

apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.ysb33r.vfs'
apply plugin: 'java'
apply plugin: 'org.asciidoctor.convert'

ext {
    revealjsVersion = '3.1.0'
    asciidoctorBackendVersion = 'master'
    downloadDir = new File(buildDir,'download')
    templateDir = new File(downloadDir,'templates')
    revealjsDir   = new File(downloadDir,'reveal.js')
}

repositories {
    jcenter()
}

dependencies {
    gems 'rubygems:slim:2.1.0'
    gems 'rubygems:thread_safe:0.3.4'
}

task download << {
    mkdir downloadDir
    vfs {
        cp "zip:https://github.com/asciidoctor/asciidoctor-reveal.js/archive/${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
                templateDir, recursive:true, overwrite:true
        cp "zip:https://github.com/hakimel/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
                revealjsDir, recursive:true, overwrite:true
    }
}

download {
    description 'Download reveal.js resources'
    outputs.dir templateDir
    outputs.dir revealjsDir
}

task copyCustomTheme(type: Copy) {
    from 'theme'
    into "$downloadDir/reveal.js/css/theme"
    include '**/netzwerg.css'
}

download.finalizedBy copyCustomTheme

asciidoctor {

    dependsOn jrubyPrepareGems

    sources {
        include '**/*.adoc'
    }

    resources {
        from (downloadDir) {
            include 'reveal.js/**'
        }
    }

    backends    'revealjs'

    attributes	'build-gradle': file('build.gradle'),
            'sourcedir': project.sourceSets.main.java.srcDirs[0],
            'endpoint-url': 'http://example.org',
            'source-highlighter' : 'highlightjs',
            'imagesdir':'./images',
            'toc':'left',
            'icons': 'font',
            'setanchors':'true',
            'idprefix':'',
            'idseparator':'-',
            'docinfo1':'true',
            'revealjs_theme':'netzwerg',
            'revealjs_transition':'linear',
            'revealjs_history':'true',
            'revealjs_slideNumber':'true',
            'revealjs_center':'false'

    options template_dirs : [new File(templateDir,'templates/slim').absolutePath ]

    dependsOn download

}

build.dependsOn asciidoctor
